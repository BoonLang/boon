operation: LATEST {
    addition_button.event.press |> THEN { Addition }
    subtraction_button.event.press |> THEN { Subtraction }
}

current_result: operation |> WHEN {
    Addition => { input_a + input_b }
    Subtraction => { input_a - input_b }
}

---------------------------------------------------

input_a: 
    Duration[seconds: 0.5]
    |> Timer/interval() 
    |> THEN { 1 } 
    |> Math/sum()

input_b: 
    Duration[seconds: 1]
    |> Timer/interval() 
    |> THEN { 10 } 
    |> Math/sum()

document: Document/new(root: Element/stripe(
    element: []
    direction: Column
    style: []
    items: LIST {
        input_a_row
        input_b_row
        addition_button
        subtraction_button
        current_result
    }
))

input_a_row: Element/stripe(
    element: []
    direction: Row
    style: []
    items: LIST { 'A: ', input_a }
)

input_b_row: Element/stripe(
    element: []
    direction: Row
    style: []
    items: LIST { 'B: ', input_b }
)

addition_button: Element/button(
    element: [event: [press: LINK]]
    style: []
    label: 'A + B'
)

subtraction_button: Element/button(
    element: [event: [press: LINK]]
    style: []
    label: 'A - B'
)
