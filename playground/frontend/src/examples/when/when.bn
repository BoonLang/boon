current_result: operation |> WHEN {
    Addition => input_a + input_b
    Subtraction => input_a - input_b
}

operation: LATEST {
    addition_button.event.press |> THEN { Addition }
    subtraction_button.event.press |> THEN { Subtraction }
}

---------------------------------------------------

input_a: sum_of_steps(step: 1, seconds: 0.5)
input_b: sum_of_steps(step: 10, seconds: 1)

FUNCTION sum_of_steps(step, seconds) {
    Duration[seconds: seconds]
    |> Timer/interval() 
    |> THEN { step } 
    |> Math/sum()
}

subtraction_button: TEXT { A - B } |> operation_button()
addition_button: TEXT { A + B } |> operation_button()

document: Document/new(root: Element/stripe(
    element: []
    direction: Column
    style: []
    items: LIST {
        input_a |> input_row(name: TEXT { A })
        input_b |> input_row(name: TEXT { B })
        addition_button
        subtraction_button
        Element/container(
            element: []
            style: [align: [row: Center]]
            child: current_result
        )
    }
))

FUNCTION input_row(input, name) {
    Element/container(
        element: []
        style: [align: [row: Center]]
        child: TEXT { {name}: {input} }
    )
}

FUNCTION operation_button(label) {
    Element/button(
        element: [event: [press: LINK]]
        style: []
        label: label
    )
}
