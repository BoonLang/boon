-- TodoMVC - Physical 3D UI Example

------------------------------------------------------------------------
-- FILTER ROUTES
------------------------------------------------------------------------

filter_routes: [
    all: '/'
    active: '/active'
    completed: '/completed'
]

------------------------------------------------------------------------
-- STORE AND STATE
------------------------------------------------------------------------

store: [
    elements: [
        filter_buttons: [
            all: LINK
            active: LINK
            completed: LINK
        ]
        remove_completed_button: LINK
        toggle_all_checkbox: LINK
        new_todo_title_text_input: LINK
    ]
    selected_filter: Router/route() |> WHEN {
        filter_routes.active => Active
        filter_routes.completed => Completed
        __ => All
    }
    go_to_result:
        LATEST {
            filter_buttons.all.event.press |> THEN { filter_routes.all }
            filter_buttons.active.event.press |> THEN { filter_routes.active }
            filter_buttons.completed.event.press |> THEN { filter_routes.completed }
        }
        |> Router/go_to()
    title_to_save: elements.new_todo_title_text_input.event.key_down.key |> WHEN {
        Enter => BLOCK {
            new_todo_title: elements.new_todo_title_text_input.text |> Text/trim()
            new_todo_title
                |> Text/empty()
                |> Bool/not()
                |> WHEN { True => new_todo_title, False => SKIP }
        }
        __ => SKIP
    }
    todos: LIST {}
        |> List/append(item: title_to_save |> new_todo())
        |> List/retain(item, if: LATEST {
            True
            item.todo_elements.remove_todo_button.event.press
                |> THEN { False }
            elements.remove_completed_button.event.press
                |> THEN { item.completed |> Bool/not() }
        })
    selected_todo_id: LATEST {
        None
        todos
            |> List/map(old, new: LATEST {
                old.todo_elements.editing_todo_title_element.event.key_down.key
                    |> WHEN { Escape => None, __ => SKIP }
                old.title_to_update
                    |> THEN { None }
                old.todo_elements.todo_title_element.event.double_click
                    |> THEN { old.id }
            })
            |> List/latest()
    }
]

------------------------------------------------------------------------
-- TODO ITEM CONSTRUCTOR
------------------------------------------------------------------------

FUNCTION new_todo(title) {
    [
        todo_elements: [
            remove_todo_button: LINK
            editing_todo_title_element: LINK
            todo_title_element: LINK
            todo_checkbox: LINK
        ]
        id: TodoId[id: Ulid/generate()]
        title_to_update:
            LATEST {
                todo_elements.editing_todo_title_element.event.blur
                    |> THEN { [] }
                todo_elements.editing_todo_title_element.event.key_down.key
                    |> WHEN { Enter => [], __ => SKIP }
            }
            |> THEN { todo_elements.editing_todo_title_element.text }
        title: LATEST {
            title
            title_to_update
        }
        completed:
            LATEST {
                False
                store.elements.toggle_all_checkbox.event.click |> THEN {
                    store.todos
                        |> List/every(item, if: item.completed)
                        |> Bool/not()
                }
            }
            |> Bool/toggle(when: todo_elements.todo_checkbox.event.click)
        edited_title: BLOCK {
            edited_title: LATEST {
                ''
                todo_elements.editing_todo_title_element.event.change.text
                title_to_update |> THEN { '' }
            }
            LATEST {
                edited_title
                todo_elements.todo_title_element.event.double_click |> THEN {
                    edited_title
                        |> Text/empty()
                        |> WHEN { True => title, False => SKIP }
                }
            }
        }
    ]
}

------------------------------------------------------------------------
-- THEME
------------------------------------------------------------------------

theme_options: [
    name: Professional
    mode: Light
]

------------------------------------------------------------------------
-- SCENE
------------------------------------------------------------------------

scene: main_scene(PASS: [store: store, theme_options: theme_options])

FUNCTION main_scene() {
    Scene/new(
        root: root_element()
        lights: Theme/lights()
        geometry: Theme/geometry()
    )
}

------------------------------------------------------------------------
-- ROOT ELEMENT
------------------------------------------------------------------------

FUNCTION root_element() {
    Element/stripe(
        element: []
        direction: Column
        gap: Theme/spacing(of: None)
        style: [
            width: Fill
            height: [sizing: Fill, minimum: Screen]
            material: Theme/material(of: Background)
        ]
        items: LIST { content_element() }
    )
}

------------------------------------------------------------------------
-- CONTENT CONTAINER
------------------------------------------------------------------------

FUNCTION content_element() {
    Element/stripe(
        element: []
        direction: Column
        gap: Theme/spacing(of: None)
        style: [
            width: [sizing: Fill, minimum: 230, maximum: 550]
            align: [row: Center]
        ]
        items: LIST {
            header()
            Element/stripe(
                element: []
                direction: Column
                gap: Theme/spacing(of: Section)
                style: [width: Fill]
                items: LIST { main_panel(), footer() }
            )
        }
    )
}

------------------------------------------------------------------------
-- HEADER
------------------------------------------------------------------------

FUNCTION header() {
    Element/block(
        element: [tag: Header]
        style: [
            padding: [top: 10]
            align: [row: Center]
            height: 130
        ]
        child: Element/text(
            element: [tag: H1]
            style: Theme/text(of: Header)
            text: 'todos'
        )
    )
}

------------------------------------------------------------------------
-- MAIN PANEL
------------------------------------------------------------------------

FUNCTION main_panel() {
    Element/stripe(
        element: [tag: Section]
        direction: Column
        gap: Theme/spacing(of: None)
        style: [
            width: Fill
            depth: Theme/depth(of: Container)
            transform: [move_closer: Theme/elevation(of: Card)]
            rounded_corners: Theme/corners(of: Comfort)
            material: Theme/material(of: Panel)
        ]
        items: LIST {
            new_todo_title_text_input()
                |> LINK { PASSED.store.elements.new_todo_title_text_input }
            PASSED.store.todos
                |> List/not_empty()
                |> WHILE {
                    True => Element/stripe(
                        element: []
                        direction: Column
                        gap: Theme/spacing(of: None)
                        style: []
                        items: LIST {
                            todos_element()
                            panel_footer()
                        }
                    )
                    False => NoElement
                }
        }
    )
}

------------------------------------------------------------------------
-- NEW TODO INPUT
------------------------------------------------------------------------

FUNCTION new_todo_title_text_input() {
    Element/text_input(
        element:[
            event: [
                change: LINK
                key_down: LINK
            ]
        ]
        style: [
            padding: [column: 19, left: 60, right: 16]
            font: Theme/font(of: Input)
            depth: Theme/depth(of: Element)
            transform: [move_further: Theme/elevation(of: Inset)]
            rounded_corners: Theme/corners(of: Touch)
            material: Theme/material(of: InputInterior[focus: element.focused])
        ]
        label: Hidden[text: 'What needs to be done?']
        text: LATEST {
            ''
            element.event.change.text
            PASSED.store.title_to_save |> THEN { '' }
        }
        placeholder: [
            style: [font: Theme/font(of: Placeholder)]
            text: 'What needs to be done?'
        ]
        focus: True
    )
}

------------------------------------------------------------------------
-- TODOS LIST
------------------------------------------------------------------------

FUNCTION todos_element() {
    Element/stack(
        element: []
        style: [width: Fill]
        layers: LIST {
            Element/stripe(
                element: []
                direction: Column
                gap: Theme/spacing(of: None)
                style: [
                    borders: Theme/border(of: Standard)
                    material: Theme/material(of: SurfaceVariant)
                ]
                items: PASSED.store.todos
                    |> List/retain(item, if: PASSED.store.selected_filter |> WHILE {
                        All => True
                        Active => item.completed |> Bool/not()
                        Completed => item.completed
                    })
                    |> List/map(old, new: old |> todo_element())
            )
            Element/block(
                element: []
                style: [
                    width: Theme/sizing(of: ToggleControl)
                    height: Fill
                    align: [row: Left, column: Top]
                ]
                child: toggle_all_checkbox()
                    |> LINK { PASSED.store.elements.toggle_all_checkbox }
            )
        }
    )
}

------------------------------------------------------------------------
-- TODO ITEM
------------------------------------------------------------------------

FUNCTION todo_element(todo) {
    Element/stripe(
        element: [
            hovered: LINK
        ]
        direction: Row
        gap: Theme/spacing(of: Tight)
        style: [
            width: Fill
            depth: Theme/depth(of: Detail)
            transform: [move_closer: 4]
            material: Theme/material(of: InputExterior[hover: element.hovered])
        ]
        items: PASSED.store.selected_todo_id = todo.id |> WHILE {
            True => LIST {
                editing_todo_title_element(todo: todo)
                    |> LINK { todo.todo_elements.editing_todo_title_element }
            }
            False => LIST {
                todo_checkbox(todo: todo)
                    |> LINK { todo.todo_elements.todo_checkbox }
                todo_title_element(todo: todo)
                    |> LINK { todo.todo_elements.todo_title_element }
                element.hovered |> WHILE {
                    True => remove_todo_button()
                        |> LINK { todo.todo_elements.remove_todo_button }
                    False => NoElement
                }
            }
        }
    )
}

------------------------------------------------------------------------
-- TOGGLE ALL CHECKBOX
------------------------------------------------------------------------

FUNCTION toggle_all_checkbox() {
    BLOCK {
        checked: PASSED.store.todos |> List/every(item, if: item.completed)

        Element/checkbox(
            element: [
                event: [click: LINK]
                hovered: LINK
            ]
            style: [
                width: Theme/sizing(of: ToggleControl)
                height: Fill
                depth: Theme/depth(of: Element)
                rounded_corners: Theme/corners(of: Touch)
                spring_range: Theme/spring_range(of: Checkbox)
                material: Theme/material(of: ToggleCheckbox[
                    checked: element.checked
                    hover: element.hovered
                ])
            ]
            label: Hidden[text: 'Toggle all']
            checked: checked
            icon: Element/block(
                style: [
                    height: 34
                    padding: [row: 27, column: 6]
                    transform: [rotate: 90, move_up: 18]
                ]
                child: Element/text(
                    style: Theme/text(of: ButtonIcon[checked: checked])
                    text: '>'
                )
            )
        )
    }
}

------------------------------------------------------------------------
-- EDITING TODO INPUT
------------------------------------------------------------------------

FUNCTION editing_todo_title_element(todo) {
    Element/text_input(
        element: [
            event: [
                blur: LINK
                change: LINK
                key_down: LINK
            ]
        ]
        style: [
            width: 506
            padding: [row: 17, top: 17, bottom: 16]
            align: Right
            font: Theme/font(of: Input)
            depth: Theme/depth(of: Element)
            rounded_corners: Theme/corners(of: Touch)
            transform: [move_closer: 24]
            material: Theme/material(of: InputInterior[focus: True])
            borders: Theme/border(of: Focus)
        ]
        label: Hidden[text: 'selected todo title']
        text: todo.edited_title
        placeholder: []
        focus: True
    )
}

------------------------------------------------------------------------
-- TODO CHECKBOX
------------------------------------------------------------------------

FUNCTION todo_checkbox(todo) {
    BLOCK {
        Element/checkbox(
            element: [
                event: [click: LINK]
                hovered: LINK
            ]
            style: [
                size: Theme/sizing(of: TouchTarget)
                depth: Theme/depth(of: Element)
                rounded_corners: Theme/corners(of: Pill)
                spring_range: Theme/spring_range(of: Checkbox)
                material: Theme/material(of: TodoCheckbox[
                    checked: todo.completed
                    hover: element.hovered
                ])
            ]
            label: Reference[element: todo.todo_elements.todo_title_element]
            checked: todo.completed
            icon: Element/block(
                element: []
                style: [
                    size: Theme/sizing(of: TouchTarget)
                    background: [url: todo.completed |> WHEN {
                        True => Assets/icon.checkbox_completed
                        False => Assets/icon.checkbox_active
                    }]
                ]
            )
        )
    }
}

------------------------------------------------------------------------
-- TODO TITLE LABEL
------------------------------------------------------------------------

FUNCTION todo_title_element(todo) {
    Element/label(
        element: [
            event: [double_click: LINK]
        ]
        style: [
            width: Fill
            padding: [column: 15, left: 15, right: 60]
            clip: Row
        ]
        label: Element/text(
            style: Theme/text(of: TodoTitle[completed: todo.completed])
            text: todo.title
        )
    )
}

------------------------------------------------------------------------
-- REMOVE TODO BUTTON
------------------------------------------------------------------------

FUNCTION remove_todo_button() {
    Element/button(
        element:[
            event: [press: LINK]
            hovered: LINK
        ]
        style: [
            size: Theme/sizing(of: TouchTarget)
            depth: Theme/depth(of: Hero)
            rounded_corners: Theme/corners(of: Pill)
            spring_range: Theme/spring_range(of: ButtonDestructive)
            material: Theme/material(of: ButtonDelete[hover: element.hovered])
        ]
        label: Element/text(
            style: Theme/text(of: ButtonDelete)
            text: '×'
        )
    )
}

------------------------------------------------------------------------
-- PANEL FOOTER
------------------------------------------------------------------------

FUNCTION panel_footer() {
    Element/stripe(
        element: [tag: Footer]
        direction: Row
        gap: Theme/spacing(of: None)
        style: [
            padding: [row: 15, column: 8]
            material: Theme/material(of: PanelFooter)
            borders: Theme/border(of: Standard)
        ]
        items: LIST {
            active_items_count_text()
            filters_element
            PASSED.store.todos |> List/any(item, if: item.completed) |> WHILE {
                True => remove_completed_button()
                    |> LINK { PASSED.store.elements.remove_completed_button }
                False => NoElement
            }
        } |> List/map(old, new: Element/block(
            element: []
            style: [width: Fill]
            child: old
        ))
    )
}

------------------------------------------------------------------------
-- ACTIVE ITEMS COUNTER
------------------------------------------------------------------------

FUNCTION active_items_count_text() {
    Element/text(
        element: []
        style: Theme/text(of: Secondary)
        text: PASSED.store.todos
            |> List/retain(item, if: item.completed |> Bool/not())
            |> List/count
            |> WHEN { count => BLOCK {
                maybe_s: count > 1 |> WHEN { True => 's', False => '' }
                '{count} item{maybe_s} left'
            }}
    )
}

------------------------------------------------------------------------
-- FILTER BUTTONS
------------------------------------------------------------------------

FUNCTION filters_element() {
    Element/stripe(
        element: []
        direction: Row
        gap: Theme/spacing(of: Standard)
        style: []
        items: LIST {
            filter_button(All)
                |> LINK { PASSED.store.elements.filter_buttons.all }
            filter_button(Active)
                |> LINK { PASSED.store.elements.filter_buttons.active }
            filter_button(Completed)
                |> LINK { PASSED.store.elements.filter_buttons.completed }
        }
    )
}

FUNCTION filter_button(filter) {
    BLOCK {
        selected: PASSED.store.selected_filter = filter
        selected_offset: selected |> WHEN { True => 6, False => 0 }

        Element/button(
            element: [
                event: [press: LINK]
                hovered: LINK
            ]
            style: [
                padding: [row: 8, column: 4]
                depth: Theme/depth(of: Element)
                rounded_corners: Theme/corners(of: Soft)
                spring_range: Theme/spring_range(of: ButtonFilter)
                transform: [move_closer: selected_offset]
                material: Theme/material(of: ButtonFilter[
                    selected: selected
                    hover: element.hovered
                ])
                outline: selected |> WHEN {
                    True => Theme/border(of: Primary)
                    False => NoOutline
                }
            ]
            label: Element/text(
                style: Theme/text(of: ButtonFilter)
                text: filter |> WHEN {
                    All => 'All'
                    Active => 'Active'
                    Completed => 'Completed'
                }
            )
        )
    }
}

------------------------------------------------------------------------
-- CLEAR COMPLETED BUTTON
------------------------------------------------------------------------

FUNCTION remove_completed_button() {
    Element/button(
        element: [
            event: [press: LINK]
            hovered: LINK
        ]
        style: [
            align: Right
            padding: [row: 4, column: 2]
            depth: Theme/depth(of: Element)
            rounded_corners: Theme/corners(of: Comfort)
            spring_range: Theme/spring_range(of: Button)
            material: Theme/material(of: ButtonClear[hover: element.hovered])
        ]
        label: Element/text(
            style: Theme/text(of: ClearButton[hover: element.hovered])
            text: 'Clear completed'
        )
    )
}

------------------------------------------------------------------------
-- FOOTER
------------------------------------------------------------------------

FUNCTION footer() {
    Element/stripe(
        element: [tag: Footer]
        direction: Column
        gap: Theme/spacing(of: Small)
        style: []
        items: LIST {
            Element/paragraph(
                element: []
                style: Theme/text(of: Small)
                contents: LIST { 'Double-click to edit a todo' }
            )
            Element/paragraph(
                element: []
                style: Theme/text(of: Small)
                contents: LIST {
                    'Created by '
                    footer_link(
                        label: 'Martin Kavík'
                        to: 'https://github.com/MartinKavik'
                    )
                }
            )
            Element/paragraph(
                element: []
                style: Theme/text(of: Small)
                contents: LIST {
                    'Part of '
                    footer_link(label: 'TodoMVC', to: 'http://todomvc.com')
                }
            )
        }
    )
}

FUNCTION footer_link(label, to) {
    Element/link(
        element: [hovered: LINK]
        style: Theme/text(of: SmallLink[hover: element.hovered])
        label: label
        to: to
        new_tab: []
    )
}
