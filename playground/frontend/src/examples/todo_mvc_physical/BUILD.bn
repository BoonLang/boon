-- TODO: file_stem function, file vs path,
-- WHEN with single arm to block refactor maybe, what is file?, item.extension -> function,
-- File/read_text() handle errors, Path/file_stem() Option handling, ..,
-- binary type?

-- Generates Assets.bn from SVG icon files

icons_directory: TEXT { ./assets/icons }
output_file: TEXT { ./Generated/Assets.bn }

svg_files: Directory/entries(icons_directory)
    |> List/retain(item, if: item.extension = TEXT { svg })
    |> List/sort_by(item, key: item.path)

generation: svg_files
    |> module_code()
    |> File/write_text(path: output_file)
    |> WHEN {
        Ok => BLOCK {
            svg_files_count: svg_files |> List/count()
            TEXT { Included {svg_files_count} icons } |> Log/info()
        }
        Error[message] => message |> Log/error()
    }

FUNCTION module_code(svg_files) {
    svg_files
        |> List/map(old, new: icon_code(file: old))
        |> Text/join_lines()
        |> WHEN { code => TEXT {
            -- Generated from {icons_directory}

            icon: [
                {code}
            ]

        } }
}

FUNCTION icon_code(file) {
    file.file_stem
        |> WHEN { name =>
            file.path
                |> File/read_text()
                |> Url/encode()
                |> WHEN { encoded => TEXT { {name}: 'data:image/svg+xml;utf8,{encoded}' } }
        }
}
