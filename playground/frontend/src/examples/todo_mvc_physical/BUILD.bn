-- Generates Assets.bn from SVG icon files

icons_directory: './assets/icons'
output_file: './Generated/Assets.bn'

svg_files: Directory/entries(icons_directory)
    |> List/retain(item, if: item.extension = 'svg')
    |> List/sort_by(item, key: item.path)

generation: svg_files
    |> module_code()
    |> File/write_text(path: output_file)
    |> WHEN {
        Ok => 'Included {svg_files |> List/count()} icons' |> Log/info()
        Error[message] => message |> Log/error()
    }

FUNCTION module_code(svg_files) {
    svg_files
        |> List/map(file, entry: icon_code(file))
        |> Text/join('\n')
        |> Text/indent(spaces: 4)
        |> THEN { code => '-- Generated by BUILD.bn\n\nicon: [\n' ++ code ++ '\n]\n' }
}

FUNCTION icon_code(file) {
    file.name
        |> Text/trim_suffix('.svg')
        |> THEN { name =>
            file.path
                |> File/read_text()
                |> URL/encode()
                |> THEN { encoded => '{name}: \'data:image/svg+xml;utf8,{encoded}\'' }
        }
}
