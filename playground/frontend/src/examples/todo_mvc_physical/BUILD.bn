-- Generates Assets.bn from SVG icon files

icons_directory: TEXT { ./assets/icons }
output_file: TEXT { ./Generated/Assets.bn }

svg_files: Directory/entries(icons_directory)
    |> List/retain(item, if: item.extension = TEXT { svg })
    |> List/sort_by(item, key: item.path)

generation: svg_files
    |> List/map(old, new: icon_code(old))
    |> Text/join_lines()
    |> WHEN { code => TEXT {
        -- Generated from {icons_directory}

        icon: [
            {code}
        ]

    } }
    |> File/write_text(path: output_file)
    |> THEN {
        count: svg_files |> List/count()
        TEXT { Included {count} icons } |> Log/info()
    }

FUNCTION icon_code(item) {
    item.path
        |> File/read_text()
        |> Url/encode()
        |> WHEN { encoded =>
            TEXT { {item.file_stem}: data:image/svg+xml;utf8,{encoded} }
        }
}
