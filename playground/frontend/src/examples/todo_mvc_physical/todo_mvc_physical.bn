store: [
    elements: [
        filter_buttons: [ 
            all: LINK
            active: LINK
            completed: LINK 
        ]
        remove_completed_button: LINK
        toggle_all_checkbox: LINK
        new_todo_title_text_input: LINK
    ]
    selected_filter: Router/route() |> WHEN {
        '/active' => Active
        '/completed' => Completed
        __ => All
    }
    go_to_result: 
        LATEST {
            filter_buttons.all.event.press |> THEN { '/' }
            filter_buttons.active.event.press |> THEN { '/active' }
            filter_buttons.completed.event.press |> THEN { '/completed' }
        } 
        |> Router/go_to()
    title_to_save: elements.new_todo_title_text_input.event.key_down.key |> WHEN {
        Enter => BLOCK {
            new_todo_title: elements.new_todo_title_text_input.text |> Text/trim()
            new_todo_title 
                |> Text/empty() 
                |> Bool/not() 
                |> WHEN { True => new_todo_title, False => SKIP }
        }
        __ => SKIP
    }
    todos: LIST {}
        |> List/append(item: title_to_save |> new_todo())
        |> List/retain(item, if: LATEST {
            True
            item.todo_elements.remove_todo_button.event.press 
                |> THEN { False }
            elements.remove_completed_button.event.press 
                |> THEN { item.completed |> Bool/not() }
        })
    selected_todo_id: LATEST {
        None
        todos
            |> List/map(old, new: LATEST {
                old.todo_elements.editing_todo_title_element.event.key_down.key
                    |> WHEN { Escape => None, __ => SKIP }
                old.title_to_update 
                    |> THEN { None }
                old.todo_elements.todo_title_element.event.double_click 
                    |> THEN { old.id }
            })
            |> List/latest()
    }
]

FUNCTION new_todo(title) {
    [
        todo_elements: [
            remove_todo_button: LINK
            editing_todo_title_element: LINK
            todo_title_element: LINK
            todo_checkbox: LINK
        ]
        id: TodoId[id: Ulid/generate()]
        title_to_update:
            LATEST {
                todo_elements.editing_todo_title_element.event.blur
                    |> THEN { [] }
                todo_elements.editing_todo_title_element.event.key_down.key 
                    |> WHEN { Enter => [], __ => SKIP }
            }
            |> THEN { todo_elements.editing_todo_title_element.text }
        title: LATEST {
            title
            title_to_update
        }
        completed: BLOCK {
            completed: LATEST {
                False
                store.elements.toggle_all_checkbox.event.click |> THEN { 
                    store.todos 
                        |> List/every(item, if: item.completed) 
                        |> Bool/not() 
                }
            }
            LATEST {
                completed
                todo_elements.todo_checkbox.event.click |> THEN { 
                    completed |> Bool/not()
                } 
            }
        }
        completed:
            LATEST {
                False
                store.elements.toggle_all_checkbox.event.click |> THEN { 
                    store.todos 
                        |> List/every(item, if: item.completed) 
                        |> Bool/not() 
                }
            }
            |> Bool/toggle(when: todo_elements.todo_checkbox.event.click)
        edited_title: BLOCK {
            edited_title: LATEST {
                ''
                todo_elements.editing_todo_title_element.event.change.text
                title_to_update |> THEN { '' }
            }
            LATEST {
                edited_title
                todo_elements.todo_title_element.event.double_click |> THEN { 
                    edited_title 
                        |> Text/empty() 
                        |> WHEN { True => title, False => SKIP } 
                }
            }
        }
    ]
}

document: Document/new(root: root_element(PASS: [store: store]))

FUNCTION simple_stage_lighting() {
    [
        sun: [
            angle: [x: -0.35, y: -0.55, z: -0.72]
            strength: Strong
            shadow_size: Large
        ]
        fill: [strength: Gentle]
    ]
}

FUNCTION root_element() {
    Element/stripe(
        element: []
        direction: Column
        gap: 0
        style: [
            width: Fill
            height: [sizing: Fill, minimum: Screen]
            font: [
                size: 14
                color: Oklch[lightness: 0.17]
                weight: Light
                family: LIST { 'Helvetica Neue', 'Helvetica', 'Arial', SansSerif }
            ]
            background: [color: Oklch[lightness: 0.97]]
            lighting: simple_stage_lighting()
        ]
        items: LIST { content_element() }
    )
}

FUNCTION content_element() {
    Element/stripe(
        element: []
        direction: Column
        gap: 0
        style: [
            width: [sizing: Fill, minimum: 230, maximum: 550]
            align: [row: Center]
            transform: [rotate_x: -4]
            elevation: 6
        ]
        items: LIST {
            header()
            Element/stripe(
                element: []
                direction: Column
                gap: 65
                style: [width: Fill]
                items: LIST { main_panel(), footer() }
            )
        }
    )
}

FUNCTION header() {
    Element/container(
        element: [tag: Header]
        style: [
            padding: [top: 10]
            align: [row: Center]
            height: 130
            font: [
                size: 100
                color: Oklch[lightness: 0.54, chroma: 0.156, hue: 21.24, alpha: 0.15]
                weight: Hairline
            ]
            elevation: 4
        ]
        child: Element/container(element: [tag: H1], style: [], child: 'todos')
    )
}

FUNCTION main_panel() {
    Element/stripe(
        element: [tag: Section]
        direction: Column
        gap: 0
        style: [
            width: Fill
            background: [color: Oklch[lightness: 0.99]]
            depth: 4
            rounded_corners: 2
            elevation: 18
            transform: [tilt: 1.5]
        ]
        items: LIST {
            new_todo_title_text_input() 
                |> LINK { PASSED.store.elements.new_todo_title_text_input }
            PASSED.store.todos 
                |> List/empty() 
                |> WHILE { 
                    True => NoElement
                    False => Stripe/new(
                        element: []
                        style: [direction: Column]
                        items: LIST { 
                            todos_element()
                            panel_footer() 
                        }
                    )
                }
        }
    )
}

FUNCTION new_todo_title_text_input() {
    Element/text_input(
        element:[
            event: [
                change: LINK
                key_down: LINK
            ]
            hovered: LINK
        ]
        style: [
            padding: [column: 19, left: 60, right: 16]
            font: [size: 24, color: Oklch[lightness: 0.42]]
            background: [color: Oklch[lightness: 0.99, alpha: 0.94]]
            depth: 6
            rounded_corners: 3
            borders: [color: Oklch[lightness: 0.7, alpha: 0.25], width: 1]
            elevation: element.hovered |> WHEN { True => 7.5, False => 6 }
        ]
        label: Hidden[text: 'What needs to be done?']
        text: LATEST { 
            ''
            element.event.change.text
            PASSED.store.title_to_save |> THEN { '' }
        }
        placeholder: [
            style: [font: [style: Italic, color: Oklch[lightness: 0.925]]]
            text: 'What needs to be done?'
        ]
        focus: True
    )

}

FUNCTION todos_element() {
    Element/stripe(
        element: [
            nearby_element: [
                position: Above
                element: toggle_all_checkbox() 
                    |> LINK { PASSED.store.elements.toggle_all_checkbox }
            ]
        ]
        direction: Column
        gap: 1
        style: [
            borders: [top: [color: Oklch[lightness: 0.915]]]
            background: [color: Oklch[lightness: 0.946]]
            depth: 2
            elevation: 14
        ]
        items: PASSED.store.todos
            |> List/retain(item, if: PASSED.store.selected_filter |> WHILE {
                All => True
                Active => item.completed |> Bool/not()
                Completed => item.completed
            })
            |> List/map(old, new: old |> todo_element())
    )
}

FUNCTION todo_element(todo) {
    Element/stripe(
        element: [hovered: LINK]
        direction: Row
        gap: 5
        style: [
            width: Fill
            background: [color: Oklch[lightness: 0.995]]
            font: [size: 24]
            depth: 3
            rounded_corners: 1.5
            elevation: element.hovered |> WHEN { True => 2, False => 1 }
        ]
        items: PASSED.store.selected_todo_id = todo.id |> WHILE {
            True => LIST { 
                editing_todo_title_element(todo: todo) 
                    |> LINK { todo.todo_elements.editing_todo_title_element }
            }
            False => LIST {
                todo_checkbox(todo: todo) 
                    |> LINK { todo.todo_elements.todo_checkbox }
                todo_title_element(todo: todo) 
                    |> LINK { todo.todo_elements.todo_title_element }
            }
        }
    )
}

FUNCTION toggle_all_checkbox() {
    Element/checkbox(
        element: [event: [click: LINK], hovered: LINK]
        style: [
            width: 60
            height: Fill
            background: [color: Oklch[lightness: 0.965]]
            depth: 8
            rounded_corners: 3
            elevation: element.hovered |> WHEN { True => 4, False => 2 }
        ]
        label: Hidden[text: 'Toggle all']
        checked: PASSED.store.todos |> List/every(item, if: item.completed)
        icon: Element/container(
            element: []
                style: [
                    height: 34
                    padding: [row: 27, column: 6]
                    font: [
                        size: 22,
                        color: Oklch[lightness: checked |> WHEN { 
                            True => 0.555
                            False => 0.92
                        }]
                    ]
                    depth: 2
                    rounded_corners: 1
                    transform: [rotate: 90, move_up: 18]
                    elevation: 1.5
                ]
            child: '>'
        )
    )
}

FUNCTION editing_todo_title_element(todo) {
    Element/text_input(
        element: [
            event: [
                blur: LINK
                change: LINK
                key_down: LINK
            ]
            hovered: LINK
        ]
        style: [
            width: 506
            padding: [row: 17, top: 17, bottom: 16]
            align: Right
            borders: [color: Oklch[lightness: 0.68]]
            font: [color: Oklch[lightness: 0.42]]
            background: [color: Oklch[lightness: 0.99]]
            depth: 6
            rounded_corners: 2
            elevation: element.hovered |> WHEN { True => 6, False => 4 }
        ]
        label: Hidden[text: 'selected todo title']
        text: todo.edited_title
        placeholder: []
        focus: True
    )
}

FUNCTION todo_checkbox(todo) {
    BLOCK {
        icon_completed: 'data:image/svg+xml;utf8,%3Csvg%20xmlns%3D%22http%3A//www.w3.org/2000/svg%22%20width%3D%2240%22%20height%3D%2240%22%20viewBox%3D%22-10%20-18%20100%20135%22%3E%3Ccircle%20cx%3D%2250%22%20cy%3D%2250%22%20r%3D%2250%22%20fill%3D%22none%22%20stroke%3D%22%23bddad5%22%20stroke-width%3D%223%22/%3E%3Cpath%20fill%3D%22%235dc2af%22%20d%3D%22M72%2025L42%2071%2027%2056l-4%204%2020%2020%2034-52z%22/%3E%3C/svg%3E'
        icon_active: 'data:image/svg+xml;utf8,%3Csvg%20xmlns%3D%22http%3A//www.w3.org/2000/svg%22%20width%3D%2240%22%20height%3D%2240%22%20viewBox%3D%22-10%20-18%20100%20135%22%3E%3Ccircle%20cx%3D%2250%22%20cy%3D%2250%22%20r%3D%2250%22%20fill%3D%22none%22%20stroke%3D%22%23ededed%22%20stroke-width%3D%223%22/%3E%3C/svg%3E'
        Element/checkbox(
            element: [event: [click: LINK], hovered: LINK]
            label: Reference[element: todo.todo_elements.todo_title_element]
            checked: todo.completed
            icon: Element/container(
                element: []
                style: [
                    size: 40
                    depth: 8
                    rounded_corners: 3
                    background: [url: checked |> WHEN { 
                        True => icon_completed
                        False => icon_active 
                    }]
                    elevation: 3
                ]
            )
        )
    }
}

FUNCTION todo_title_element(todo) {
    Element/label(
        element: [
            event: [double_click: LINK]
            hovered: LINK
            nearby_element: [
                position: Right
                element: hovered |> WHILE {
                    True => remove_todo_button()
                        |> LINK { todo.todo_elements.remove_todo_button }
                    False => NoElement
                }
            ]
        ]
        style: [
            width: Fill
            font: [
                size: 24
                line: [strike: todo.completed]
                color: Oklch[lightness: todo.completed |> WHEN { 
                    True => 0.885
                    False => 0.42 
                }]
            ]
            padding: [column: 15, left: 15, right: 60]
            clip: Row
            depth: 2
            rounded_corners: 1
            background: [color: Oklch[lightness: todo.completed |> WHEN {
                True => 0.92
                False => 0.8
            }, alpha: 0.1]]
            elevation: element.hovered |> WHEN { True => 3, False => 2 }
        ]
        label: todo.title
    )
}

FUNCTION remove_todo_button() {
    Element/button(
        element:[
            event: [press: LINK]
            hovered: LINK
        ]
        style: [
            size: 40
            depth: 10
            rounded_corners: 3
            background: [color: Oklch[lightness: 0.97]]
            borders: [color: Oklch[lightness: 0.68, alpha: 0.5], width: 1]
            transform: [
                move_left: 50
                move_down: 14
            ]
            elevation: element.hovered |> WHEN { True => 7, False => 4 }
            font: [
                size: 30
                align: Center
                color: element.hovered |> WHEN {
                    True => Oklch[lightness: 0.570, chroma: 0.109, hue: 18.87]
                    False => Oklch[lightness: 0.733, chroma: 0.060, hue: 18.62]
                }
            ]
        ]
        label: '×'
    )
}

FUNCTION panel_footer() {
    Element/stripe(
        element: [tag: Footer]
        direction: Row
        gap: 0
        style: [
            padding: [row: 15, column: 8]
            font: [color: Oklch[lightness: 0.57]]
            borders: [top: [color: Oklch[lightness: 0.925]]]
            background: [color: Oklch[lightness: 0.965]]
            depth: 5
            rounded_corners: 2
            elevation: 12
        ]
        items: LIST {
            active_items_count_text
            filters_element
            PASSED.store.todos |> List/any(item, if: item.completed) |> WHILE {
                True => remove_completed_button() 
                    |> LINK { PASSED.store.elements.remove_completed_button }
                False => NoElement
            }
        } |> List/map(old, new: Element/container(
            element: []
            style: [width: Fill]
            child: old
        ))
    )
}

FUNCTION active_items_count_text() {
    PASSED.store.todos 
        |> List/retain(item, if: item.completed |> Bool/not()) 
        |> List/count
        |> WHEN { count => BLOCK {
            maybe_s: count > 1 |> WHEN { True => 's', False => '' }
            '{count} item{maybe_s} left'
        }}
}

FUNCTION filters_element() {
    Element/stripe(
        element: []
        direction: Row
        gap: 10
        style: [
            elevation: 14
            depth: 3
        ]
        items: LIST {
            filter_button(All) 
                |> LINK { PASSED.store.elements.filter_buttons.all }
            filter_button(Active) 
                |> LINK { PASSED.store.elements.filter_buttons.active }
            filter_button(Completed) 
                |> LINK { PASSED.store.elements.filter_buttons.completed }
        }
    )
}

FUNCTION filter_button(filter) {
    Element/button(
        element: [
            event: [press: LINK]
            hovered: LINK
        ]
        style: [
            padding: [row: 8, column: 14]
            depth: 10
            rounded_corners: 3
            background: [
                color: PASSED.store.selected_filter = filter |> WHEN {
                    True => Oklch[lightness: 0.93, chroma: 0.14, hue: 24]
                    False => Oklch[lightness: 0.97]
                }
            ]
            borders: [color: Oklch[lightness: 0.72, alpha: 0.6], width: 1]
            elevation: BLOCK {
                selected: PASSED.store.selected_filter = filter
                rest_height: selected |> WHEN { True => 6, False => 4 }
                hover_height: selected |> WHEN { True => 8, False => 6 }
                element.hovered |> WHEN { True => hover_height, False => rest_height }
            }
        ]
        label: filter |> WHEN {
            All => 'All'
            Active => 'Active'
            Completed => 'Completed'
        }
    )
}

FUNCTION remove_completed_button() {
    Element/button(
        element: [
            event: [press: LINK]
            hovered: LINK
        ]
        style: [
            align: Right
            padding: [row: 6, column: 12]
            depth: 8
            rounded_corners: 3
            background: [color: Oklch[lightness: 0.965]]
            borders: [color: Oklch[lightness: 0.7, alpha: 0.4], width: 1]
            elevation: element.hovered |> WHEN { True => 6, False => 4 }
            font: [line: [underline: element.hovered], color: Oklch[lightness: 0.55]]
        ]
        label: 'Clear completed'
    )
}

FUNCTION footer() {
    Element/stripe(
        element: [tag: Footer]
        direction: Column
        gap: 9
        style: [
            font: [size: 10, align: Center, color: Oklch[lightness: 0.805]]
            background: [color: Oklch[lightness: 0.97]]
            depth: 4
            rounded_corners: 2
            elevation: 10
        ]
        items: LIST {
            Element/paragraph(
                element: []
                style: []
                contents: LIST { 'Double-click to edit a todo' }
            )
            Element/paragraph(
                element: []
                style: []
                contents: LIST {
                    'Created by '
                    footer_link(
                        label: 'Martin Kavík'
                        to: 'https://github.com/MartinKavik'
                    )
                }
            )
            Element/paragraph(
                element: []
                style: []
                contents: LIST {
                    'Part of '
                    footer_link(label: 'TodoMVC', to: 'http://todomvc.com')
                }
            )
        }
    )
}

FUNCTION footer_link(label, to) {
    Element/link(
        element: [hovered: LINK]
        style: [
            font: [line: [underline: element.hovered]]
            depth: 3
            rounded_corners: 2
            background: [color: element.hovered |> WHEN {
                True => Oklch[lightness: 0.9, chroma: 0.08, hue: 240]
                False => Oklch[lightness: 0.97]
            }]
            elevation: element.hovered |> WHEN { True => 4, False => 2 }
        ]
        label: label
        to: to
        new_tab: []
    )
}
