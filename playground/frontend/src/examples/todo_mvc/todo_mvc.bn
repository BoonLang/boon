store: [
    elements: [
        filter_buttons: [
            all: LINK
            active: LINK
            completed: LINK
        ]
        remove_completed_button: LINK
        toggle_all_checkbox: LINK
        new_todo_title_text_input: LINK
    ]
    navigation_result: LATEST {
        elements.filter_buttons.all.event.press
            |> THEN { TEXT { / } }
        elements.filter_buttons.active.event.press
            |> THEN { TEXT { /active } }
        elements.filter_buttons.completed.event.press
            |> THEN { TEXT { /completed } }
    } |> Router/go_to()
    selected_filter: Router/route() |> WHILE {
        TEXT { / } => All
        TEXT { /active } => Active
        TEXT { /completed } => Completed
        __ => All
    }
    title_to_add:
        -- key_down event now contains both .key and .text fields.
        -- Matching full pattern [key: Enter, text: t] to capture text directly.
        elements.new_todo_title_text_input.event.key_down
        |> WHEN {
            [key: Enter, text: t] => BLOCK {
                trimmed: t |> Text/trim()
                trimmed
                    |> Text/is_not_empty()
                    |> WHEN { True => trimmed, False => SKIP }
            }
            __ => SKIP
        }
    todos:
        LIST {
            new_todo(title: TEXT { Buy groceries })
            new_todo(title: TEXT { Clean room })
        }
        |> List/append(item: title_to_add |> new_todo())
        |> List/remove(
            item
            on: item.todo_elements.remove_todo_button.event.press
        )
        |> List/remove(
            item
            on: elements.remove_completed_button.event.press
                |> THEN {
                    item.completed
                        |> WHEN { True => [], False => SKIP }
                }
        )
    todos_count: todos |> List/count()
    completed_todos_count:
        todos
        |> List/retain(item, if: item.completed)
        |> List/count()
    active_todos_count: todos_count - completed_todos_count
    all_completed: todos_count == completed_todos_count
]

FUNCTION new_todo(title) {
    [
        todo_elements: [
            remove_todo_button: LINK
            editing_todo_title_element: LINK
            todo_title_element: LINK
            todo_checkbox: LINK
        ]
        title: title |> HOLD state { BLOCK {
            edit_el: todo_elements.editing_todo_title_element
            -- confirm_title uses edit_el.text for blur events (text is stable by then)
            confirm_title: edit_el.text
                |> Text/trim()
                |> WHEN {
                    t => t
                        |> Text/is_not_empty()
                        |> WHEN { True => t, False => SKIP }
                }
            LATEST {
                -- For Enter, use key_down.text to avoid data race.
                -- Use destructuring pattern [key: Enter, text: t] to capture text directly,
                -- same pattern that works for new_todo_title_text_input.
                edit_el.event.key_down |> WHEN {
                    [key: Enter, text: t] => t
                        |> Text/trim()
                        |> WHEN {
                            trimmed => trimmed
                                |> Text/is_not_empty()
                                |> WHEN { True => trimmed, False => SKIP }
                        }
                    __ => SKIP
                }
                edit_el.event.blur |> THEN { confirm_title }
            }
        }}
        editing: False |> HOLD state { BLOCK {
            edit_el: todo_elements.editing_todo_title_element
            LATEST {
                todo_elements.todo_title_element.event.double_click
                    |> THEN { True }
                edit_el.event.key_down |> WHEN {
                    [key: Enter] => False
                    [key: Escape] => False
                    __ => SKIP
                }
                edit_el.event.blur |> THEN { False }
            }
        }}
        completed: False |> HOLD state {
            LATEST {
                todo_elements.todo_checkbox.event.click
                    |> THEN { state |> Bool/not() }
                store.elements.toggle_all_checkbox.event.click
                    |> THEN { store.all_completed |> Bool/not() }
            }
        }
    ]
}

document: Document/new(root: root_element(PASS: [store: store]))

FUNCTION root_element() {
    Element/stripe(
        element: []
        direction: Column
        gap: 0
        style: [
            width: Fill
            height: Fill
            align: [row: Center]
            font: [
                size: 14
                color: Oklch[lightness: 0.17]
                weight: Light
                family: LIST {
                    TEXT { Helvetica Neue }
                    TEXT { Helvetica }
                    TEXT { Arial }
                    SansSerif
                }
            ]
            background: [color: Oklch[lightness: 0.97]]
        ]
        items: LIST { content_element() }
    )
}

FUNCTION content_element() {
    Element/stripe(
        element: []
        direction: Column
        gap: 0
        style: [
            width: 550
            align: [row: Center]
        ]
        items: LIST {
            header()
            Element/stripe(
                element: []
                direction: Column
                gap: 65
                style: [width: Fill]
                items: LIST { main_panel(), footer() }
            )
        }
    )
}

FUNCTION header() {
    Element/container(
        element: [tag: Header]
        style: [
            padding: [top: 10, bottom: 20]
            align: [row: Center]
            height: 130
            font: [
                size: 100
                color: Oklch[
                    lightness: 0.54
                    chroma: 0.156
                    hue: 21.24
                    alpha: 0.15
                ]
                weight: Hairline
            ]
        ]
        child: Element/container(
            element: [tag: H1]
            style: []
            child: TEXT { todos }
        )
    )
}

FUNCTION main_panel() {
    Element/stripe(
        element: [tag: Section]
        direction: Column
        gap: 0
        style: [
            shadows: LIST {
                [y: 2, blur: 4, color: Oklch[alpha: 0.2]]
                [y: 25, blur: 50, color: Oklch[alpha: 0.1]]
            }
            width: Fill
            background: [color: Oklch[lightness: 1]]
        ]
        items: LIST {
            Element/stripe(
                element: []
                direction: Row
                gap: 0
                style: [width: Fill]
                items: LIST {
                    PASSED.store.todos |> List/is_empty() |> WHILE {
                        True => NoElement
                        False => toggle_all_checkbox()
                            |> LINK { PASSED.store.elements.toggle_all_checkbox }
                    }
                    new_todo_title_text_input()
                        |> LINK { PASSED.store.elements.new_todo_title_text_input }
                }
            )
            PASSED.store.todos
                |> List/is_empty()
                |> WHILE {
                    True => NoElement
                    False => Element/stripe(
                        element: []
                        direction: Column
                        gap: 0
                        style: []
                        items: LIST {
                            todos_element()
                            panel_footer()
                        }
                    )
                }
        }
    )
}

FUNCTION new_todo_title_text_input() {
    Element/text_input(
        element: [
            event: [
                change: LINK
                key_down: LINK
            ]
        ]
        style: [
            width: Fill
            padding: [column: 19, left: 16, right: 16]
            font: [size: 24, color: Oklch[lightness: 0.42]]
            background: [color: Oklch[alpha: 0.003]]
            shadows: LIST {
                [
                    direction: Inwards
                    y: -2
                    blur: 1
                    color: Oklch[alpha: 0.03]
                ]
            }
        ]
        label: Hidden[
            text: TEXT { What needs to be done? }
        ]
        text: LATEST {
            Text/empty()
            element.event.change.text
            PASSED.store.title_to_add |> THEN { Text/empty() }
        }
        placeholder: [
            style: [
                font: [
                    style: Italic
                    color: Oklch[lightness: 0.925]
                ]
            ]
            text: TEXT { What needs to be done? }
        ]
        focus: True
    )

}

FUNCTION todo_item(todo) {
    Element/stripe(
        element: [hovered: LINK]
        direction: Row
        gap: 5
        style: [
            width: Fill
            background: [color: Oklch[lightness: 1]]
            font: [size: 24]
            padding: [row: 15, column: 10]
        ]
        items: LIST {
            todo_checkbox(todo: todo)
            todo.editing |> WHILE {
                True => Element/text_input(
                    element: [
                        event: [
                            change: LINK
                            key_down: LINK
                            blur: LINK
                        ]
                    ]
                    style: [
                        width: Fill
                        font: [
                            size: 24
                            color: Oklch[lightness: 0.42]
                        ]
                        padding: [row: 5, column: 2]
                        outline: [
                            side: Inner
                            color: Oklch[
                                lightness: 0.5
                                chroma: 0.1
                                hue: 220
                            ]
                        ]
                    ]
                    label: Hidden[
                        text: TEXT { Edit todo }
                    ]
                    text: LATEST {
                        todo.title
                        element.event.change.text
                    }
                    placeholder: []
                    focus: True
                ) |> LINK {
                    todo.todo_elements.editing_todo_title_element
                }

                False => Element/label(
                    element: [event: [double_click: LINK]]
                    style: [
                        width: Fill
                        font: [
                            size: 24
                            color: Oklch[lightness: 0.42]
                            line: [strikethrough: todo.completed]
                        ]
                    ]
                    label: todo.title
                ) |> LINK {
                    todo.todo_elements.todo_title_element
                }
            }
            element.hovered |> WHILE {
                True =>
                    remove_todo_button()
                    |> LINK {
                        todo.todo_elements.remove_todo_button
                    }
                False => NoElement
            }
        }
    )
}

FUNCTION todos_element() {
    Element/stripe(
            element: [tag: Ul]
            direction: Column
            gap: 0
            style: [width: Fill]
            items:
                PASSED.store.todos
                |> List/retain(
                    item
                    if: PASSED.store.selected_filter |> WHILE {
                        All => True
                        Active => item.completed |> Bool/not()
                        Completed => item.completed
                    }
                )
                |> List/map(item, new: todo_item(todo: item))
    )
}

FUNCTION toggle_all_checkbox() {
    BLOCK {
        all_completed: PASSED.store.all_completed
        Element/checkbox(
            element: [event: [click: LINK]]
            style: [width: 60, height: Fill]
            label: Hidden[text: TEXT { Toggle all }]
            checked: all_completed
            icon: Element/container(
                element: []
                style: [
                    height: 34
                    padding: [row: 27, column: 6]
                    font: [
                        size: 22
                        color: Oklch[
                            lightness: all_completed |> WHEN {
                                True => 0.40
                                False => 0.75
                            }
                        ]
                    ]
                    transform: [rotate: 90]
                ]
                child: TEXT { ❯ }
            )
        )
    }
}

FUNCTION todo_checkbox(todo) {
    BLOCK {
        icon_completed: TEXT { data:image/svg+xml;utf8,%3Csvg%20xmlns%3D%22http%3A//www.w3.org/2000/svg%22%20width%3D%2240%22%20height%3D%2240%22%20viewBox%3D%22-10%20-18%20100%20135%22%3E%3Ccircle%20cx%3D%2250%22%20cy%3D%2250%22%20r%3D%2250%22%20fill%3D%22none%22%20stroke%3D%22%23bddad5%22%20stroke-width%3D%223%22/%3E%3Cpath%20fill%3D%22%235dc2af%22%20d%3D%22M72%2025L42%2071%2027%2056l-4%204%2020%2020%2034-52z%22/%3E%3C/svg%3E }
        icon_active: TEXT { data:image/svg+xml;utf8,%3Csvg%20xmlns%3D%22http%3A//www.w3.org/2000/svg%22%20width%3D%2240%22%20height%3D%2240%22%20viewBox%3D%22-10%20-18%20100%20135%22%3E%3Ccircle%20cx%3D%2250%22%20cy%3D%2250%22%20r%3D%2250%22%20fill%3D%22none%22%20stroke%3D%22%23ededed%22%20stroke-width%3D%223%22/%3E%3C/svg%3E }
        Element/checkbox(
            element: [event: [click: LINK]]
            style: []
            label: Reference[
                element: todo.todo_elements.todo_title_element
            ]
            checked: todo.completed
            icon: Element/container(
                element: []
                style: [
                    size: 40
                    background: [url: todo.completed |> WHEN {
                        True => icon_completed
                        False => icon_active
                    }]
                ]
                child: NoElement
            )
        )
        |> LINK { todo.todo_elements.todo_checkbox }
    }
}

FUNCTION remove_todo_button() {
    Element/button(
        element: [
            event: [press: LINK]
            hovered: LINK
        ]
        style: [
            size: 40
            font: [
                size: 30
                align: Center
                color: element.hovered |> WHEN {
                    True => Oklch[
                        lightness: 0.570
                        chroma: 0.109
                        hue: 18.87
                    ]
                    False => Oklch[
                        lightness: 0.733
                        chroma: 0.060
                        hue: 18.62
                    ]
                }
            ]
        ]
        label: TEXT { × }
    )
}

FUNCTION panel_footer() {
    Element/stripe(
        element: [tag: Footer]
        direction: Row
        gap: 0
        style: [
            width: Fill
            padding: [row: 15, column: 10]
            font: [color: Oklch[lightness: 0.57]]
            borders: [top: [color: Oklch[lightness: 0.925]]]
            shadows: LIST {
                [y: 1, blur: 1, color: Oklch[alpha: 0.2]]
                [y: 8, spread: -3, color: Oklch[lightness: 0.973]]
                [y: 9, blur: 1, spread: -3, color: Oklch[alpha: 0.2]]
                [y: 16, spread: -6, color: Oklch[lightness: 0.973]]
                [y: 17, blur: 2, spread: -6, color: Oklch[alpha: 0.2]]
            }
        ]
        items: LIST {
            footer_section(
                align: Start
                child: active_items_count_text()
            )
            footer_section(
                align: Center
                child: filters_element()
            )
            footer_section(
                align: End
                child: PASSED.store.completed_todos_count > 0 |> WHILE {
                    True => remove_completed_button()
                        |> LINK { PASSED.store.elements.remove_completed_button }
                    False => NoElement
                }
            )
        }
    )
}

FUNCTION footer_section(align, child) {
    Element/stripe(
        element: []
        direction: Row
        gap: 0
        style: [width: Fill, align: [row: align]]
        items: LIST { child }
    )
}

FUNCTION active_items_count_text() {
    -- Pattern match directly to avoid dataflow glitch where plural_suffix
    -- computation races with TEXT interpolation (engine_v2 timing issue)
    PASSED.store.active_todos_count |> WHEN {
        1 => TEXT { 1 item left }
        count => TEXT { {count} items left }
    }
}

FUNCTION filters_element() {
    Element/stripe(
        element: []
        direction: Row
        gap: 10
        style: []
        items: LIST {
            filter_button(filter: All)
                |> LINK {
                    PASSED.store.elements.filter_buttons.all
                }
            filter_button(filter: Active)
                |> LINK {
                    PASSED.store.elements.filter_buttons.active
                }
            filter_button(filter: Completed)
                |> LINK {
                    PASSED.store.elements.filter_buttons.completed
                }
        }
    )
}

FUNCTION filter_button(filter) {
    Element/button(
        element: [
            event: [press: LINK]
            hovered: LINK
        ]
        style: [
            padding: [row: 8, column: 4]
            rounded_corners: 3
            -- Workaround for BLOCK reactive chain bug:
            -- Inline the selected_filter comparison directly to avoid
            -- stale BLOCK local variables. When selected_filter changes,
            -- the WHILE will re-evaluate both its condition and body.
            outline:
                PASSED.store.selected_filter == filter
                |> Bool/or(that: element.hovered)
                |> WHILE {
                    True => [
                        side: Inner
                        color: Oklch[
                            lightness: 0.5
                            chroma: 0.165
                            hue: 25.36
                            -- Compute alpha inline: 0.2 if selected, 0.1 if only hovered
                            alpha: PASSED.store.selected_filter == filter |> WHEN {
                                True => 0.2
                                False => 0.1
                            }
                        ]
                    ]
                    False => NoOutline
                }
        ]
        label: filter |> WHEN {
            All => TEXT { All }
            Active => TEXT { Active }
            Completed => TEXT { Completed }
        }
    )
}

FUNCTION remove_completed_button() {
    Element/button(
        element: [
            event: [press: LINK]
            hovered: LINK
        ]
        style: [
            font: [line: [underline: element.hovered]]
        ]
        label: TEXT { Clear completed }
    )
}

FUNCTION footer() {
    Element/stripe(
        element: [tag: Footer]
        direction: Column
        gap: 9
        style: [
            font: [
                size: 10
                align: Center
                color: Oklch[lightness: 0.805]
            ]
        ]
        items: LIST {
            Element/paragraph(
                element: []
                style: []
                contents: LIST {
                    TEXT { Double-click to edit a todo }
                }
            )
            Element/paragraph(
                element: []
                style: []
                contents: LIST {
                    TEXT { Created by }
                    Text/space()
                    footer_link(
                        label: TEXT { Martin Kavík }
                        to: TEXT { https://github.com/MartinKavik }
                    )
                }
            )
            Element/paragraph(
                element: []
                style: []
                contents: LIST {
                    TEXT { Part of }
                    Text/space()
                    footer_link(
                        label: TEXT { TodoMVC }
                        to: TEXT { http://todomvc.com }
                    )
                }
            )
        }
    )
}

FUNCTION footer_link(label, to) {
    Element/link(
        element: [hovered: LINK]
        style: [
            font: [
                line: [underline: element.hovered]
            ]
        ]
        label: label
        to: to
        new_tab: []
    )
}
